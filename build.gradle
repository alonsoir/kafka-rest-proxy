/*
 *  Copyright 2016 Markus Helbig
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

// === Repository config ===

repositories {
    mavenCentral()
}

// === JAVA configuration ===

//noinspection GroovyUnusedAssignment
sourceCompatibility = JavaVersion.VERSION_1_8
//noinspection GroovyUnusedAssignment
targetCompatibility = JavaVersion.VERSION_1_8

// === Integration Test Configuration ===

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    //noinspection GroovyAccessibility
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    it.mustRunAfter test
}
check.dependsOn integrationTest

// === JAR ===

jar {
    manifest {
        attributes("Implementation-Title": "basic-project",
                "Implementation-Version": version)
    }
}

// === Sources JAR ===

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// === JavaDoc JAR ===

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    //noinspection GroovyAccessibility
    from javadoc.destinationDir
}

// === Catch all artifacts ===

artifacts {
    archives sourcesJar
    archives javadocJar
}

// === Dependencies ===

dependencies {
    compile 'io.dropwizard:dropwizard-core:1.0.0'
    compile 'org.apache.kafka:kafka-clients:0.10.0.1'
    //testing
    testCompile 'io.dropwizard:dropwizard-testing:1.0.0'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    //integrationTest
    integrationTestCompile 'io.dropwizard:dropwizard-client:1.0.0'
    integrationTestCompile('org.apache.kafka:kafka_2.11:0.10.0.1') {
        exclude module: 'slf4j-log4j12'
    }
    integrationTestCompile 'org.apache.curator:curator-test:3.2.0'
}


// === Application configuration ===

//noinspection GroovyUnusedAssignment
mainClassName = 'org.mh.kafka.rest.proxy.KafkaRestProxy'

run {
    //noinspection GroovyAssignabilityCheck
    args 'server', 'src/conf/kafka-rest-proxy.yml'
}

distributions {
    main {
        contents {
            from('src/conf') {
                into 'conf'
            }
        }
    }
}

// === IDEA configuration ===

idea {
    module {
        scopes.TEST.plus += [configurations.integrationTestCompile, configurations.integrationTestRuntime]
    }
}

// === Wrapper generation ===

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}
